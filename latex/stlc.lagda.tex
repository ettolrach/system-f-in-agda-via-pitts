\begin{code}
module stlc where
\end{code}
\begin{comment}
  \begin{code}
  -- Data types (naturals, strings, characters)
  open import Data.Nat using (ℕ; zero; suc; _<_; _≥_; _≤_; _≤?_; _<?_; z≤n; s≤s; _⊔_)
    renaming (_≟_ to _≟ℕ_)
  open import Data.Nat.Properties using (≤-refl; ≤-trans; ≤-<-trans; <-≤-trans; ≤-antisym; ≤-total;
    +-mono-≤; n≤1+n; m≤n⇒m≤1+n; suc-injective; <⇒≢; ≰⇒>; ≮⇒≥)
  open import Data.String using (String; fromList) renaming (_≟_ to _≟str_; _++_ to _++str_;
    length to str-length; toList to ⟪_⟫)
  open import Data.Char using (Char)
  open import Data.Char.Properties using () renaming (_≟_ to _≟char_)
  
  -- Function manipulation.
  open import Function using (_∘_; flip; it; id; case_returning_of_)
  
  -- Relations and predicates/decidability.
  import Relation.Binary.PropositionalEquality as Eq
  open Eq using (_≡_; _≢_; refl; sym; trans; cong; cong-app; cong₂)
  open Eq.≡-Reasoning using (begin_; step-≡-∣; step-≡-⟩; _∎)
  open import Relation.Binary.Definitions using (DecidableEquality)
  open import Relation.Nullary.Decidable using (Dec; yes; no; True; False; toWitnessFalse;
    toWitness; fromWitness; ¬?; ⌊_⌋; From-yes)
  open import Relation.Unary using (Decidable)
  open import Relation.Binary using () renaming (Decidable to BinaryDecidable)
  open import Relation.Nullary.Negation using (¬_; contradiction)
  open import Data.Empty using (⊥-elim)
  
  -- Products and exists quantifier.
  open import Data.Product using (_×_; proj₁; proj₂; ∃-syntax) renaming (_,_ to ⟨_,_⟩)
  open import Data.Sum using (_⊎_; inj₁; inj₂)
  
  -- Lists.
  open import Data.List using (List; []; _∷_; _++_; length; filter; map; foldr; head; replicate)
  open import Data.List.Properties using (≡-dec)
  import Data.List.Membership.DecPropositional as DecPropMembership
  open import Data.List.Relation.Unary.All using (All; all?; lookup)
    renaming (fromList to All-fromList; toList to All-toList)
  open import Data.List.Relation.Unary.Any using (Any; here; there)
  open import Data.List.Extrema Data.Nat.Properties.≤-totalOrder using (max; xs≤max)
  
  -- Import list membership using List Char comparisons.
  private
    _≟lchar_ : ∀ (xs ys : List Char) → Dec (xs ≡ ys)
    xs ≟lchar ys = ≡-dec (_≟char_) xs ys
  
  open DecPropMembership _≟lchar_ using (_∈_; _∉_; _∈?_)
  \end{code}
\end{comment}
The remaining properties of STLC which were not proven in \ref{appendix:tspl} include some
substitution properties and creating an evaluator. Since it is a necessary prelude to System F, this
new work is presented here. As explained in section \ref{background:evaluation_strategy}, one
evaluation strategy is weak-head reduction, which is also used by
\citet[chapter~Properties]{wadler_programming_2022} and \citet[section~5]{chargueraud_locally_2012}.
Thus, evaluation will be restricted to closed terms only (those without free variables).
\begin{code}
  open import plfa_adaptions
  open import tspl_prior_work
  open import cofinite
\end{code}

To avoid name conflicts with Agda's reserved keywords, the syntax of the metalangauge is different
to how it may be commonly written. We will use substitutes, like ƛ for $\lambda$, or $\cdot$ for
function application (which is usually left implicit).

\section{Substitution Preserves Types}
A good example of the induction principle for cofinite quantification is the following proof, that
all well-typed expressions are locally closed using induction on the type judgement.

\begin{code}
  ⊢⇒lc : ∀ {Γ t A} → Γ ⊢ t ⦂ A → LocallyClosed t
  ⊢⇒lc {Γ} {t} {A} (⊢free Γ∋A) = free-lc
  ⊢⇒lc {Γ} {ƛ t} {A} (⊢ƛ И⟨ Иe₁ , Иe₂ ⟩) j =
    И⟨ Иe₁ , (λ a {a∉} → cong ƛ_
      (open-rec-lc-lemma
        (λ ())
        (open-rec-lc (⊢⇒lc (Иe₂ a {a∉}))))) ⟩
  ⊢⇒lc {Γ} {t₁ · t₂} (⊢· ⊢A⇒B ⊢A) _ =
    И⟨ domain Γ , (λ _ → cong₂ _·_
      (open-rec-lc (⊢⇒lc ⊢A⇒B)) (open-rec-lc (⊢⇒lc ⊢A))) ⟩
  ⊢⇒lc {Γ} {‵zero} ⊢zero = ‵zero-≻
  ⊢⇒lc {Γ} {‵suc t} (⊢suc ⊢t) j =
    И⟨ domain Γ , (λ a {a∉} →
      cong ‵suc_ (open-rec-lc (⊢⇒lc ⊢t))) ⟩
\end{code}

Recall the definition of local closure (see equation \ref{equation:local_closure}). In Agda,
\begin{minted}{agda}
  _≻_ : ℕ → Term → Set
  i ≻ t = (j : ℕ) ⦃ _ : j ≥ i ⦄ → И a , ([ j —→ a ] t ≡ t)
\end{minted}
Given a $j \in \nat$, we need to show that for cofinite $a$, opening $t$ with $[j \to a]$ will leave
$t$ unchanged. This is why each case includes the extra \texttt{j} argument.

For all except the $\lambda$-abstraction case, the inductive hypothesis is straight-forward. For
$\lambda$-abstractions, we are given a list \texttt{Иe₁} and a property \texttt{Иe₂} which states
that $(\Gamma , \, x \colon A \vdash [0 \to x] t \colon B)$ for some \texttt{List Char} $x \not \in
\texttt{Иe₁}$. We need to provide a proof that $\lambda \, t$ is locally closed. By including the
argument \texttt{j}, we now only need to show that $\cof a, \; (\lambda \, ([j + 1 \to a] t) =
\lambda \, t)$. The inductive hypothesis allows us to call \texttt{⊢⇒lc} on \texttt{Иe₂}, or in
other words, it implies that for cofinite $a$, the term $[0 \to a] t$ is locally closed. After
applying the inductive hypothesis, the proof is a consequence of a previous lemma
\texttt{open-rec-lc} which was proven in appendix \ref{appendix:tspl}.

Now we need show that substituting preserves types. As mentioned, we restrict ourselves to only
closed terms for substitution.
\begin{code}
  {-# TERMINATING #-}
  subst : ∀ {Γ x t u A B}
    → ∅ ⊢ u ⦂ A
    → Γ , x ⦂ A ⊢ t ⦂ B
      --------------------
    → Γ ⊢ [ x := u ] t ⦂ B
  subst {x = y} ⊢u (⊢free {x = x} (H refl)) with y ≟lchar y
  ... | yes _   = weaken ⊢u
  ... | no  y≢y = contradiction refl y≢y
  subst {x = y} ⊢u (⊢free {x = x} (T x≢y ∋x)) with y ≟lchar x
  ... | yes y≡x = contradiction (sym y≡x) x≢y
  ... | no  _   = ⊢free ∋x
  subst {x = x} {t = ƛ t} ⊢u (⊢ƛ И⟨ Иe₁ , Иe₂ ⟩) =
    ⊢ƛ И⟨ x ∷ Иe₁
        , (λ a {a∉} →
          let a≢y   = ∉∷[]⇒≢ (proj₁ (∉-++ a∉))
              a∉Иe₁ = proj₂ (∉-++ a∉)
          in subst-open-context
            {t = t}
            (sym-≢ a≢y)
            (⊢⇒lc ⊢u)
            (subst ⊢u (swap a≢y (Иe₂ a {a∉Иe₁}))) )
        ⟩
  subst ⊢u (⊢· ⊢t₁ ⊢t₂) = ⊢· (subst ⊢u ⊢t₁) (subst ⊢u ⊢t₂)
  subst ⊢u ⊢zero = ⊢zero
  subst ⊢u (⊢suc ⊢t) = ⊢suc (subst ⊢u ⊢t)
\end{code}

The property is proven by induction on the type judgement of the term \texttt{M}. Agda cannot
determine the termination of this function, and the problematic call is when the type judgement is a
$\lambda$-abstraction \texttt{ƛ M}. Specifically, it highlights the problematic code to be
\texttt{subst ⊢u (swap a≢y (Иe₂ a {a∉Иe₁}))}. Thus, I will only detail that
step\footnote{Technically, the Agda compiler also highlits a problematic call for the application
case, but this is caused by the problematic $\lambda$-abstraction case, so is irrelevant.}.

Since we are inducting on the type judgement, the inductive hypothesis for a term \texttt{ƛ M} of
type \texttt{A ⇒ A'} states that the property $P$ holds for $P(\texttt{И⟨ Иe₁ , Иe₂ ⟩})$. Let
\texttt{b} be an appropriate \texttt{List Char} to supply to \texttt{Иe₂}, then it will return a
proof that \texttt{Γ , x ⦂ A , b ⦂ A' ⊢ [ 0 —→ b ] t ⦂ B}. In this case, the \texttt{subst} function
is called on \texttt{Иe₂} (with a \texttt{swap} function applied, but since this function doesn't
call \texttt{subst} and only operates on the context, this call is irrelevant to this termination
issue). Since we are deconstructing the type judgement and are calling \texttt{subst} on the term
\texttt{Иe₂} which makes up the input type judgement, this function call corresponds to the
inductive hypothesis, and is thus valid.

Substituting a term for an index is similar to the definition of the free-variable substitution
(defined in appendix \ref{appendix:substitution_proofs}). This is, confusingly, also called
`opening' by \citet{chargueraud_locally_2012}.
\begin{code}
  [_:→_]_ : ℕ → Term → Term → Term
  [ k :→ u ] (free x) = free x
  [ k :→ u ] (bound i) with k ≟ℕ i
  ... | yes _ = u
  ... | no  _ = bound i
  [ k :→ u ] (ƛ t) = ƛ [ (suc k) :→ u ] t
  [ k :→ u ] (t₁ · t₂) = [ k :→ u ] t₁ · [ k :→ u ] t₂
  [ k :→ u ] ‵zero = ‵zero
  [ k :→ u ] (‵suc t) = ‵suc ([ k :→ u ] t)
\end{code}

Using an index $i$ to open with $x \in \texttt{List Char}$ is the same as using the index
substitution with the term \texttt{free $x$}.
% Unused.
\begin{code}
  —→≡:→free : ∀ {i : ℕ} {x : List Char} (t : Term)
    → [ i —→ x ] t ≡ [ i :→ free x ] t
  —→≡:→free {i} {x} (free y) = refl
  —→≡:→free {i} {x} (bound k) with i ≟ℕ k
  ... | yes _ = refl
  ... | no  _ = refl
  —→≡:→free {i} {x} (ƛ t) = cong ƛ_ (—→≡:→free t)
  —→≡:→free {i} {x} (t₁ · t₂) =
    cong₂ _·_ (—→≡:→free t₁) (—→≡:→free t₂)
  —→≡:→free {i} {x} ‵zero = refl
  —→≡:→free {i} {x} (‵suc t) = cong ‵suc_ (—→≡:→free t)
\end{code}

There are quite a few more properties of index substitution which \citet{chargueraud_locally_2012}
proves, but the only relevant one for evaluation is \texttt{subst-intro}. It proves that
substituting a term for an index is the same as first opening the term with an $x \in \texttt{List
Char}$ and then using the free variable substitution using this $x$.
\begin{code}
  subst-intro : ∀ {x : List Char} {i : ℕ} (t u : Term)
    → x # t
    → [ i :→ u ] t ≡ [ x := u ] ([ i —→ x ] t)
  subst-intro {x} (free y) u x#t with x ≟lchar y
  ... | yes refl with () ← x#t
  ... | no  x≢y  = refl
  subst-intro {x} {i} (bound j) u x#t with i ≟ℕ j
  ... | no  i≢j  = refl
  ... | yes refl with x ≟lchar x
  ...   | yes refl = refl
  ...   | no  x≢x  = contradiction refl x≢x
  subst-intro (ƛ t) u x#ƛt = cong ƛ_ (subst-intro t u (#-ƛ t x#ƛt))
  subst-intro {x} (t₁ · t₂) u x#t =
    let ⟨ x#t₁ , x#t₂ ⟩ = #-· t₁ t₂ x#t in
      cong₂ _·_ (subst-intro t₁ u x#t₁) (subst-intro t₂ u x#t₂)
  subst-intro ‵zero u x#t = refl
  subst-intro (‵suc t) u x#st =
    cong ‵suc_ (subst-intro t u (#-‵suc t x#st))
\end{code}

Since we need to replace bound variables for free ones to perform a $\beta$-reduction, we should
prove that this substitution preserves types.
\begin{code}
  subst-op : ∀ {Γ t u A B}
    → ∅ ⊢ u ⦂ A
    → Γ ⊢ ƛ t ⦂ A ⇒ B
      --------------------
    → Γ ⊢ [ 0 :→ u ] t ⦂ B
  subst-op {t = t} {u = u} ⊢u (⊢ƛ И⟨ Иe₁ , Иe₂ ⟩) =
    let x                  = fresh (fv t ++ Иe₁)
        ⟨ x∉fv-t , x∉Иe₁ ⟩ = ∉-++ {xs = fv t} {ys = Иe₁}
                                (fresh-correct (fv t ++ Иe₁))
    in ≡-with-⊢ (subst ⊢u (Иe₂ x {x∉Иe₁}))
      (sym (subst-intro t u (∉fv⇒# x t (x∉fv-t))))
\end{code}

\section{Substitution Commutes}
Here we prove a property which...

None of these functions are necessary for evaluation, so we can postulate function extensionality,
that is, for some sets $A$ and $B$ and functions $f, g \colon A \to B$, 
\begin{equation*}
  (\forall x \in A, \, f(x) = g(x)) \implies f = g.
\end{equation*}

This is the 4\textsuperscript{th} axiom in the elementary
theory of the category of sets \citep{tom_leinster_rethinking_2014}. It's known that this does not
cause an inconsistency in Agda's system of logic \citep{wadler_programming_2022}, however, since it
needs to be postulated, we cannot call this function. So, we leave it only defined in this module.

\begin{code}
  -- module substitution_commutes where
  --   private postulate
  --     extensionality : ∀ {A B : Set} {f g : A → B}
  --       → (∀ (x : A) → f x ≡ g x)
  --         -----------------------
  --       → f ≡ g

  --   free-inferred : ∀ {x : List Char} → Term
  --   free-inferred {x} = free x

  --   ctx-weaken : ∀ {Γ x y A B} → x ≢ y → Γ , y ⦂ B ∋ x ⦂ A → Γ ∋ x ⦂ A
  --   ctx-weaken x≢y (H refl) = contradiction refl x≢y
  --   ctx-weaken x≢y (T _ ∋x) = ∋x

  --   exts : ∀ {Γ Δ x y A B}
  --     → (Γ ∋ x ⦂ A        → ∃[ L ]        (Δ ⊢ L ⦂ A))
  --       ------------------------------------------------------------
  --     → (Γ , y ⦂ B ∋ x ⦂ A → ∃[ L ] (Δ , y ⦂ B ⊢ L ⦂ A))
  --   exts σ (H refl) = ⟨ free-inferred , (⊢free (H refl)) ⟩
  --   exts σ (T x≢y x) = ⟨ proj₁ (σ x) , rename (T {!!}) (proj₂ (σ x)) ⟩

    -- subst : ∀ {Γ Δ}
    --   → (∀ {x A} → Γ ∋ x ⦂ A → Δ ⊢ A)
    --     -----------------------
    --   → (∀ {x A} → Γ ⊢ A → Δ ⊢ A)
    -- subst σ (` x)          =  σ x
\end{code}

\section{Evaluation}
Using weak-head reduction, only $\lambda$-abstractions are values, together with the two primitives
that were introduced.
\begin{code}
  data Value : Term → Set where
    V-ƛ : ∀ {t} → Value (ƛ t)
    V-zero : Value ‵zero
    V-suc : ∀ {t} → Value t → Value (‵suc t)
\end{code}

\citet{chargueraud_locally_2012} adds another requirement for $\lambda$-abstractions: $1 \succ M$,
or in other words, that $\lambda M$ is locally closed. However, since we are only evaluating
well-typed terms, and all well-typed terms are locally closed (see section
\ref{appendix:type_judgements}), this requirement isn't necessary here.

We follow the rules for small-step reduction given in \citet{chargueraud_locally_2012}. These are
encoded in Agda below.
\begin{code}
  infix 4 _—→_
  data _—→_ : Term → Term → Set where
    ξ₁ : ∀ {t₁ t₁' t₂}
      → t₁ —→ t₁'
      → LocallyClosed t₂
        -------------------
      → t₁ · t₂ —→ t₁' · t₂

    ξ₂ : ∀ {t₁ t₂ t₂'}
      → t₂ —→ t₂'
        ---------
      → t₁ · t₂ —→ t₁ · t₂'

    ξ-suc : ∀ {t t'}
      → t —→ t'
        ------------------
      → ‵suc t —→ ‵suc t'

    β : ∀ {t u}
      → 1 ≻ t
      → Value u
        -------
      → (ƛ t) · u —→ [ 0 :→ u ] t
\end{code}
Once again, the requirements for local closure could be removed, but they are kept here to follow
the rules presented in \citet{chargueraud_locally_2012}.

Following \citet{wadler_programming_2022}, we define some convenience functions, namely, reflexive
and transitive closure properties which will help reason about taking a reduction step. These follow
similar syntax to how equality reasoning is written in the Agda standard library
\citep{the_agda_community_agda_2024}.
\begin{comment}
\begin{code}
  infix  2 _—↠_
  infix  1 begin'_
  infixr 2 _—→⟨_⟩_
  infix  3 _∎'
\end{code}
\end{comment}
\begin{code}
  data _—↠_ : Term → Term → Set where
    _∎' : ∀ M
        ---------
      → M —↠ M

    step—→ : ∀ L {M N}
      → M —↠ N
      → L —→ M
        ---------
      → L —↠ N

  pattern _—→⟨_⟩_ L L—→M M—↠N = step—→ L M—↠N L—→M

  begin'_ : ∀ {M N}
    → M —↠ N
      ------
    → M —↠ N
  begin' M—↠N = M—↠N
\end{code}

There are two important properties which are required to implement evaluation. Progress (that terms
can always take a step, or are a value and are thus finished reducing) is presented below.
\begin{code}
  data Progress (t : Term) : Set where
    step : ∀ {t'}
      → t —→ t'
        ----------
      → Progress t

    done :
        Value t
        ----------
      → Progress t

  progress : ∀ {t A}
    → ∅ ⊢ t ⦂ A
      ----------
    → Progress t
  progress (⊢ƛ x) = done V-ƛ
  progress (⊢· ⊢t₁ ⊢t₂) with progress ⊢t₁
  ... | step t₁→t₁' = step (ξ₁ t₁→t₁' (⊢⇒lc ⊢t₂))
  ... | done V-ƛ with progress ⊢t₂
  ...   | step t₂→t₂' = step (ξ₂ t₂→t₂')
  ...   | done val    = step (β (i≻ƛt⇒si≻t (⊢⇒lc ⊢t₁)) val)
  progress ⊢zero = done V-zero
  progress (⊢suc ⊢t) with progress ⊢t
  ... | step t→t' = step (ξ-suc t→t')
  ... | done val  = done (V-suc val)
\end{code}

And preservation, that types are preserved when reducing.
\begin{code}
  preserve : ∀ {t t' A}
    → ∅ ⊢ t ⦂ A
    → t —→ t'
      ----------
    → ∅ ⊢ t' ⦂ A
  preserve (⊢· ⊢t₁ ⊢t₂) (ξ₁ t→t' _) = ⊢· (preserve ⊢t₁ t→t') ⊢t₂
  preserve (⊢· ⊢t₁ ⊢t₂) (ξ₂ t→t') = ⊢· ⊢t₁  (preserve ⊢t₂ t→t')
  preserve (⊢· ⊢t₁ ⊢t₂) (β x x₁) = subst-op ⊢t₂ ⊢t₁
  preserve (⊢suc ⊢t) (ξ-suc t→t') = ⊢suc (preserve ⊢t t→t')
\end{code}

Since STLC is not Turing complete \citep{church_formulation_1940}, we don't need to worry about
programs which don't terminate. Still, the evaluation function needs to receive a timeout argument,
as otherwise, Agda cannot determine that it would terminate. Thus, we define a record which limits
evaluation to a certain number of reduction steps. Then we can use the preserve and progress
properties to make an \texttt{eval} function. This is the same definition as
\citet{wadler_programming_2022} uses, so the explanation is ommitted here.
\begin{code}
  record Gas : Set where
    eta-equality
    constructor gas
    field
      amount : ℕ

  data Finished (t : Term) : Set where
    done : Value t → Finished t
    out-of-gas : Finished t

  data Steps (t : Term) : Set where
    steps : ∀ {t'} → t —↠ t' → Finished t' → Steps t

  eval : ∀ {t A} → Gas → ∅ ⊢ t ⦂ A → Steps t
  eval {t} (gas zero) ⊢t = steps (t ∎') out-of-gas
  eval {t} (gas (suc n)) ⊢t with progress ⊢t
  ... | done V-t = steps (t ∎') (done V-t)
  ... | step {t'} t→t' with eval (gas n) (preserve ⊢t t→t')
  ...   | steps t'→u fin-u = steps (t —→⟨ t→t' ⟩ t'→u) fin-u
\end{code}

We provide an example for evaluation. First, we require a type derivation for $2+2$. We would show
that it evaluates to $4$, but because the evaluation proof requires more than eleven thousand lines
of code, it is omitted. But we encourage the reader to try it out for themselves. The proofs of
\texttt{⊢two} and \texttt{⊢plus} are long and are omitted (but present in the source file).
\begin{code}
  two : Term
  two = ƛ ƛ bound 1 · (bound 1 · bound 0)

  plus : Term
  plus = ƛ ƛ ƛ ƛ bound 3 · bound 1 · (bound 2 · bound 1 · bound 0)

  suc' : Term
  suc' = ƛ ‵suc (bound 0)

  ⊢two : ∅ ⊢ two ⦂ (‵ℕ ⇒ ‵ℕ) ⇒ ‵ℕ ⇒ ‵ℕ
  -- omitted.

  ⊢plus : ∀ {Γ A} → Γ ⊢ plus ⦂
    ((A ⇒ A) ⇒ A ⇒ A) ⇒ ((A ⇒ A) ⇒ A ⇒ A) ⇒ ((A ⇒ A) ⇒ A ⇒ A)
  -- omitted

  ⊢suc' : ∀ {Γ} → Γ ⊢ suc' ⦂ ‵ℕ ⇒ ‵ℕ
  ⊢suc' = ⊢ƛ И⟨ [] , (λ _ → ⊢suc (⊢free H′)) ⟩

  ⊢2+2 : ∅ ⊢ plus · two · two · suc' · ‵zero ⦂ ‵ℕ
  ⊢2+2 = ⊢· (⊢· (⊢· (⊢· ⊢plus  ⊢two) ⊢two) ⊢suc') ⊢zero

  -- Using Emacs, normalise "eval (gas 100) ⊢2+2" by pressing
  -- C-c C-n.
\end{code}
\begin{comment}
\begin{code}
  ⊢two = ⊢ƛ
    И⟨ []
    , (λ a → ⊢ƛ
      И⟨ (a ∷ [])
      , (λ b {b∉} →
        ⊢·
        (⊢free (T (sym-≢ (∉∷[]⇒≢ b∉)) H′))
        (⊢· (⊢free (T (sym-≢ (∉∷[]⇒≢ b∉)) H′)) (⊢free (H′)))) ⟩) ⟩

  ⊢plus = ⊢ƛ
    И⟨ []
    , (λ a → ⊢ƛ
      И⟨ a ∷ []
      , (λ b {b∉} → ⊢ƛ
        И⟨ a ∷ b ∷ []
        , (λ c {c∉} → ⊢ƛ
          И⟨ a ∷ b ∷ c ∷ []
          , (λ d {d∉} →
          ⊢·
            (⊢·
              (⊢free (T (a≢d d∉) (T (a≢c c∉) (T (a≢b b∉) H′))))
              (⊢free (T (c≢d d∉) (H′))))
            (⊢·
              (⊢·
                (⊢free (T (b≢d d∉) (T (b≢c c∉) H′)))
                (⊢free (T (c≢d d∉) H′)))
              (⊢free H′))) ⟩) ⟩) ⟩) ⟩
    where
      a≢d : ∀ {a b c d} → d ∉ a ∷ b ∷ c ∷ [] → a ≢ d
      a≢d d∉ = sym-≢ (∉∷[]⇒≢ (proj₁ (∉-++ d∉)))
      a≢c : ∀ {a b c} → c ∉ a ∷ b ∷ [] → a ≢ c
      a≢c c∉ = sym-≢ (∉∷[]⇒≢ (proj₁ (∉-++ c∉)))
      a≢b : ∀ {a b} → b ∉ a ∷ [] → a ≢ b
      a≢b b∉ = sym-≢ (∉∷[]⇒≢ b∉)
      c≢d : ∀ {a b c d} → d ∉ a ∷ b ∷ c ∷ [] → c ≢ d
      c≢d {a} {b} d∉ =
        sym-≢ (∉∷[]⇒≢ (proj₂ (∉-++ {xs = a ∷ b ∷ []} d∉)))
      b≢d : ∀ {a b c d} → d ∉ a ∷ b ∷ c ∷ [] → b ≢ d
      b≢d {a} {b} d∉ =
        sym-≢ (∉∷[]⇒≢ (proj₂ (
          ∉-++
            {xs = a ∷ []}
            (proj₁ (∉-++ {xs = a ∷ b ∷ []} d∉)))))
      b≢c : ∀ {a b c} → c ∉ a ∷ b ∷ [] → b ≢ c
      b≢c {a} c∉ = sym-≢ (∉∷[]⇒≢ (proj₂ (∉-++ {xs = a ∷ []} c∉)))
\end{code}
\end{comment}

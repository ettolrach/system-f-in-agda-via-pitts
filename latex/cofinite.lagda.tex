Before describing locally nameless representation, we need to define \textit{cofinite
quantification}.

This quantification uses the symbol $\cof$. We write, for some (finite or countably infinite) set $X$ and property $P$,
\begin{equation}
  \label{equation:cofinite}
  \cof x \in X, P(x) \iff
  \exists S \subsetneq X, \, S \text{ is finite}, \, \forall x \in (X \setminus S), \, P(x).
\end{equation}

That is, the property $P$ may or may not hold for some finite subset $S \subseteq X$, but for all
elements $x \in (X \setminus S)$, the property $P(x)$ holds.

To motivate this quantification, consider the following example which is very similar to a problem
that arises in the next section. Let $P$ and $Q$ be properties of a term $L$, each of which hold for
all fresh identifiers. Suppose we wish to prove that there exists some identifier $z$ for which both
properties hold. Written formally,
\begin{equation*}
  \forall x, y \notin \fv(L), \; P(L, x) \wedge Q(L, y)
  \implies \exists z \; \text{such that} \; P(L, z) \wedge Q(L, z),
\end{equation*}
where $\text{fv} \colon \text{Terms} \to \text{Identifiers}$ returns all identifiers of free
variables in a term.

Since there are only a finite number of free variables in a term, we can always find a fresh
identifier. Informally, the proof is trivial since we can choose a fresh identifier such that $z = x
= y$.

Proof-assistants like Agda model universal quantification as an argument to a function. In this
example, $x$ and $y$ would be arguments of type Identifier. These are arbitrary, as in, we cannot
make any assumptions on these arguments other than their type
\citep[chapter~Quantifier]{wadler_programming_2022}. We would additionally be given evidence that $x
\notin \fv(L)$ and that $P(L, x)$ holds, and that $y \notin \fv(L)$ and that $Q(L, x)$ holds.
Importantly, unlike the informal proof, we cannot set $x = y$.

To solve this, we can use cofinite quantification. By writing
\begin{equation*}
  \cof x, y, \; P(L, x) \wedge Q(L, y)
  \implies \exists z \; \text{such that} \; P(L, z) \wedge Q(L, z),
\end{equation*}
Agda will now give us a proof that for any $x \notin S_1$ and $y \notin S_2$ for some sets $S_1$ and
$S_2$, the properties $P(L, x)$ and $Q(L, y)$ hold. We now choose a $z \notin S_1 \cup
S_2$ (note that as both $S_1$ and $S_2$ are by definition finite, this doesn't require the
axiom of choice). This $z$ will satisfy $P(L, z)$ since $z \notin S_1$ and it will also satisfy
$Q(L, z)$ since $z \notin S_2$.

This technique of combining several different `disallowed' sets will be used in chapter
\ref{chapter3} for theorems such as \texttt{rename}.

While the Agda implementation that \citet{pitts_locally_2023} gives uses an arbitrary set, I chose
to use the specific \texttt{List Char}, since we will only quantify over strings. This is abstracted
using a type alias to a type called \texttt{Id}. I also use \texttt{List} instead of \texttt{FSet}
(finite set) since lists will suffice for our purposes. The rest of the definition is adapted from
his definition of cofinite quantification.
\begin{code}
module cofinite where
\end{code}
\begin{comment}
\begin{code}
  -- Data types (naturals, strings, characters)
  open import Data.Nat using (ℕ; zero; suc; _≤_; z≤n; s≤s)
  open import Data.Nat.Properties using (≤-refl; ≤-trans; ≤-antisym; ≤-total)
  open import Data.Char using (Char)
  open import Data.Char.Properties using () renaming (_≟_ to _≟char_)

  -- Relations and predicates/decidability.
  import Relation.Binary.PropositionalEquality as Eq
  open Eq using (_≡_; _≢_; refl; sym; trans; cong)
  open import Relation.Binary.Definitions using (DecidableEquality)
  open import Relation.Nullary.Decidable using (Dec)
  open import Relation.Unary using (Decidable)
  open import Relation.Binary using () renaming (Decidable to BinaryDecidable)
  open import Relation.Nullary.Negation using (contradiction)

  -- Lists.
  open import Data.List using (List; []; _∷_; _++_; length; filter; map; foldr; head; replicate)
  open import Data.List.Properties using (≡-dec)
  import Data.List.Membership.DecPropositional as DecPropMembership
  open import Data.List.Relation.Unary.All using (All; all?; lookup)
    renaming (fromList to All-fromList; toList to All-toList)
  open import Data.List.Relation.Unary.Any using (Any; here; there)
  open import Data.List.Extrema Data.Nat.Properties.≤-totalOrder using (max; xs≤max)

  -- Import list membership using List Char comparisons.
  private
    _≟lchar_ : ∀ (xs ys : List Char) → Dec (xs ≡ ys)
    xs ≟lchar ys = ≡-dec (_≟char_) xs ys

  open DecPropMembership _≟lchar_ using (_∈_; _∉_; _∈?_)
\end{code}
\end{comment}
\begin{code}
  private
    Id : Set
    Id = List Char

  record Cof (P : Id → Set) : Set where
    inductive
    eta-equality
    constructor И⟨_,_⟩
    field
      Иe₁ : List Id
      Иe₂ : (a : Id) {_ : a ∉ Иe₁} → P a
  open Cof public

  Cof-syntax : (P : Id → Set) → Set
  Cof-syntax = Cof
  syntax Cof-syntax (λ a → P) = И a , P
\end{code}
